//===----------------------------------------------------------------------===//
// Copyright Â© 2025 Apple Inc. and the Pkl project authors. All rights reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//   https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//===----------------------------------------------------------------------===//

amends "@pkl.impl.ghactions/PklCI.pkl"

import "@com.github.actions/actions/setup-node/v6/SetupNode.pkl"
import "@com.github.actions/catalog.pkl"
import "@com.github.actions/Workflow.pkl"
import "@pkl.impl.ghactions/steps/SetupPkl.pkl"

local setupNode: SetupNode = new {
  with {
    `node-version` = "24"
    `registry-url` = "https://registry.npmjs.org"
  }
}

local buildAndTest: Workflow = new {
  jobs {
    ["build-and-test"] {
      `runs-on` = "ubuntu-latest"
      steps {
        (catalog.`actions/checkout@v6`) {}
        setupNode
        new { run = "npm ci" }
        new { run = "make lint-js" }
        new {
          // Note: we don't use `@com.gitub.actions/jobs/HawkeyeJob` because our `make build` script
          // will run `hawkeye format`.
          name = "Setup hawkeye"
          // language=bash
          run =
            """
            DIR="$(mktemp -d /tmp/hawkeye-XXXXXX)"
            curl -L -o hawkeye.tar.gz "https://github.com/korandoru/hawkeye/releases/download/v6.3.0/hawkeye-x86_64-unknown-linux-gnu.tar.xz"
            tar -xvf hawkeye.tar.gz -C "$DIR"
            echo "$DIR/hawkeye-x86_64-unknown-linux-gnu/" >> "$GITHUB_PATH"
            echo "$DIR" >> "$GITHUB_PATH"
            """
        }
        // language=bash
        new {
          run =
            """
            make build
            """
        }
        new {
          run =
            """
            git diff --name-only --exit-code || echo "Some files were incorrectly formatted!"
            """
        }
      }
    }
    ["test-pkl-formatting"] {
      `runs-on` = "ubuntu-latest"
      steps {
        (catalog.`actions/checkout@v6`) {}
        new SetupPkl { version = "0.30.1" }.step
        new { run = "pkl format --diff-name-only ." }
      }
    }
  }
}

prb = buildAndTest

build = buildAndTest

main = buildAndTest

releaseBranch = buildAndTest

release {
  jobs {
    ["publish"] {
      `runs-on` = "ubuntu-latest"
      permissions {
        contents = "write"
        `id-token` = "write"
      }
      steps {
        ...buildAndTest.jobs["build-and-test"].steps
        new {
          name = "Publish to npm"
          run = "npm publish"
        }
        new {
          name = "gh release"
          env {
            ["GIT_TAG"] = Workflow.context.github.refName
            ["GIT_SHA"] = Workflow.context.github.sha
            ["GH_TOKEN"] = Workflow.context.github.token
          }
          // language=bash
          run =
            #"""
            gh release create "${GIT_TAG}" \
              --title "${GIT_TAG}" \
              --target "${GIT_SHA}" \
              --verify-tag \
              --notes "Release notes: https://github.com/apple/highlightjs-pkl/blob/main/CHANGELOG.adoc#release-${GIT_TAG}"
            """#
        }
      }
    }
  }
}
